plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

jacoco {
    toolVersion = "0.8.5"
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
// Do not change this version number
version buildNumber

sourceCompatibility = 11
targetCompatibility = 11

repositories {

    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

// region static code analysis

checkstyle {
    maxWarnings = 0
    toolVersion = '8.10.1'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
}

pmd {
    toolVersion = "6.19.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-pdf-email-common'
            version project.version
        }
    }
}

def versions = [
    reformLogging: '5.1.7'
]

dependencies {
    implementation group: 'com.github.hmcts', name: 'java-logging', version: versions.reformLogging
    implementation group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '7.0.0'

    implementation 'com.github.hmcts:sscs-common:4.5.12'

    implementation("com.github.hmcts:ccd-case-document-am-client:1.7.1")
    // Remove this dependency once the secure doc-store is in use
    implementation group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '7.0.0'

    implementation group: 'org.springframework', name: 'spring-context-support', version: '5.3.12'
    implementation group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.14.0'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.1.0'

    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.1.0'
    implementation group: 'org.apache.tika', name: 'tika-parsers', version: '2.1.0'
    implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.14'
    implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff', version: '3.7.0'
    implementation group: 'org.apache.pdfbox', name: 'xmpbox', version: '2.0.24'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.24'
    implementation group: 'pl.touk', name: 'throwing-function', version: '1.3'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.2'

    implementation 'org.projectlombok:lombok:1.18.22'
    annotationProcessor "org.projectlombok:lombok:1.18.22"

    testImplementation 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor "org.projectlombok:lombok:1.18.22"
    testImplementation group: 'junit', name: 'junit', version: 4.12
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'

}
