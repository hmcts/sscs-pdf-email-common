plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.2'
    id "org.owasp.dependencycheck" version "8.3.1"
}

jacoco {
    toolVersion = "0.8.10"
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
// Do not change this version number
version buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {

    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

// region static code analysis

pmd {
    toolVersion = "6.55.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 7-10 fails the build, anything lower and it doesn't fail the build
    failBuildOnCVSS = 7.0
    suppressionFile = 'config/owasp/suppressions.xml'

    cveValidForHours = 4

    analyzers {
        retirejs {
            enabled = false
        }
        ossIndex {
            enabled = false
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-pdf-email-common'
            version project.version
        }
    }
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.15.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.2'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.15.2'
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.15.2'

    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.4.12'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.12'

    implementation group: 'com.github.hmcts', name: 'java-logging', version: '5.1.9'
    implementation group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '7.0.1'

    implementation group: 'com.github.hmcts', name: 'sscs-common', version: '5.2.6'

    implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'
    // Remove this dependency once the secure doc-store is in use
    implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3'

    implementation group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.14.2'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    //code needs changing if this version number gets bumped
    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.8.0'
    implementation group: 'org.apache.tika', name: 'tika-parsers', version: '2.8.0'
    implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.20'
    implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff', version: '3.9.4'
    implementation group: 'org.apache.pdfbox', name: 'xmpbox', version: '2.0.29'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.29'
    implementation group: 'pl.touk', name: 'throwing-function', version: '1.3'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.11.0'

    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

    implementation group: 'org.projectlombok', name:'lombok', version: '1.18.28'
    annotationProcessor group: 'org.projectlombok', name:'lombok', version: '1.18.28'

    testImplementation group: 'org.projectlombok', name:'lombok', version: '1.18.28'
    testAnnotationProcessor group: 'org.projectlombok', name:'lombok', version: '1.18.28'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.10.0'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.10.0'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.10.0'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.10.0'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.4.0'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
}

dependencyManagement {
    dependencies {
        // resolves: CVE-2020-28052
        dependency group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
        dependency group: 'commons-io', name: 'commons-io', version: '2.13.0'

        dependencySet(group: 'org.springframework.boot', version: '2.7.12') {
            entry 'spring-boot-starter-web'
            entry 'spring-boot-starter-actuator'
            entry 'spring-boot-starter-aop'
            entry 'spring-boot-starter-validation'
        }

        dependencySet(group: 'org.springframework', version: '5.3.27') {
            entry 'spring-aop'
            entry 'spring-aspects'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-context-support'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-jdbc'
            entry 'spring-jms'
            entry 'spring-messaging'
            entry 'spring-orm'
            entry 'spring-test'
            entry 'spring-tx'
            entry 'spring-web'
            entry 'spring-webmvc'
        }

        //CVE-2021-22112
        dependencySet(group: 'org.springframework.security', version: '5.8.3') {
            entry 'spring-security-crypto'
        }
    }
}
