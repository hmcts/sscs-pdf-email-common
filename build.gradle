plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.owasp.dependencycheck" version "10.0.3"
}

jacoco {
    toolVersion = "0.8.10"
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
// Do not change this version number
version buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {

    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

// region static code analysis

pmd {
    toolVersion = "6.55.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = layout.buildDirectory.dir("reports/pmd").get().asFile
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 7-10 fails the build, anything lower and it doesn't fail the build
    failBuildOnCVSS = 7.0
    suppressionFile = 'config/owasp/suppressions.xml'

    nvd {
        validForHours = 24
        delay = 8000
        apiKey = System.getenv("NVD_API_KEY")
    }

    analyzers {
        retirejs {
            enabled = false
        }
        ossIndex {
            enabled = false
        }
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.required.set(true)
    }
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-pdf-email-common'
            version project.version
        }
    }
}

def versions = [
        jackson      : '2.17.2',
        junitjupiter : '5.11.0-M2',
        lombok       : '1.18.34'
]

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson

    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.6'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.6'

    implementation group: 'com.github.hmcts', name: 'java-logging', version: '6.1.6'
    implementation group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '7.0.1'

    implementation group: 'com.github.hmcts', name: 'sscs-common', version: '5.12.29', {
        exclude group: 'junit', module: 'junit'
    }

    implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'
    // Remove this dependency once the secure doc-store is in use
    implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.0.3'

    implementation group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.14.4'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1', {
        exclude group: 'junit', module: 'junit'
    }
    //code needs changing if this version number gets bumped
    implementation group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.9.2'
    implementation group: 'org.apache.tika', name: 'tika-parsers', version: '2.9.2'
    implementation group: 'net.coobird', name: 'thumbnailator', version: '0.4.20'
    implementation group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff', version: '3.11.0'
    implementation group: 'org.apache.pdfbox', name: 'xmpbox', version: '2.0.31'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.31'
    implementation group: 'pl.touk', name: 'throwing-function', version: '1.3'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.12.0'

    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.0'

    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0-M2'

    implementation group: 'org.projectlombok', name:'lombok', version: versions.lombok
    annotationProcessor group: 'org.projectlombok', name:'lombok', version: versions.lombok

    implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.9.5-SPRING2'

    testImplementation group: 'org.projectlombok', name:'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name:'lombok', version: versions.lombok

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junitjupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junitjupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junitjupiter
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: versions.junitjupiter
    testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.11.0-M2'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.11.0-M2'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.12.0'
    testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
}

dependencyManagement {
    dependencies {
        // CVE-2023-2976, CVE-2020-8908
        dependency group: 'com.google.guava', name: 'guava', version: '33.2.1-jre'

        // CVE-2020-13956
        dependency group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'

        dependencySet(group: 'org.springframework.boot', version: '2.7.18') {
            entry 'spring-boot-starter-actuator'
        }

        dependencySet(group: 'org.springframework', version: '5.3.37') {
            entry 'spring-web'
        }

        // CVE-2021-22044
        dependencySet(group: 'org.springframework.cloud', version: '3.1.9') {
            entry 'spring-cloud-starter-openfeign'
        }
    }
}
