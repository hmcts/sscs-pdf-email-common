plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'maven'
    id "org.owasp.dependencycheck" version "7.2.1"
}

jacoco {
    toolVersion = "0.8.5"
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group 'com.github.hmcts'
// Do not change this version number
version buildNumber

sourceCompatibility = 11
targetCompatibility = 11

repositories {

    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }

    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

// region static code analysis

checkstyle {
    maxWarnings = 0
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.50.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 7-10 fails the build, anything lower and it doesn't fail the build
    failBuildOnCVSS = 7.0
    suppressionFile = 'config/owasp/suppressions.xml'

    cveValidForHours = 24

    analyzers {
        retirejs {
            enabled = false
        }
        ossIndex {
            enabled = false
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'sscs-pdf-email-common'
            version project.version
        }
    }
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.0-rc1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.0-rc1'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.14.0-rc1'
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: '2.14.0-rc1'

    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.11'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.11'

    compile group: 'com.github.hmcts', name: 'java-logging', version: '5.1.9'
    compile group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '7.0.1'

    implementation group: 'com.github.hmcts', name: 'sscs-common', version: '4.19.0'

    implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'
    // Remove this dependency once the secure doc-store is in use
    compile group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'

    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    compile group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.14.0'
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    //code needs changing if this version number gets bumped
    compile group: 'com.github.sps.junidecode', name: 'junidecode', version: '0.3'
    compile group: 'org.apache.tika', name: 'tika-core', version: '2.4.1'
    compile group: 'org.apache.tika', name: 'tika-parsers', version: '2.4.1'
    compile group: 'net.coobird', name: 'thumbnailator', version: '0.4.16'
    compile group: 'com.twelvemonkeys.imageio', name: 'imageio-tiff', version: '3.8.1'
    compile group: 'org.apache.pdfbox', name: 'xmpbox', version: '2.0.25'
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.25'
    compile group: 'pl.touk', name: 'throwing-function', version: '1.3'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.33'

    compileOnly group: 'org.projectlombok', name:'lombok', version: '1.18.22'
    annotationProcessor group: 'org.projectlombok', name:'lombok', version: '1.18.22'

    testCompileOnly group: 'org.projectlombok', name:'lombok', version: '1.18.22'
    testAnnotationProcessor group: 'org.projectlombok', name:'lombok', version: '1.18.22'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.1'
    testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.9.1'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.9.1'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.9.1'

    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'
}

dependencyManagement {
    dependencies {
        // resolves: CVE-2020-28052
        dependency group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.69'
        dependency group: 'commons-io', name: 'commons-io', version: '2.11.0'

        dependencySet(group: 'org.springframework.boot', version: '2.7.4') {
            entry 'spring-boot-starter-web'
            entry 'spring-boot-starter-actuator'
            entry 'spring-boot-starter-aop'
            entry 'spring-boot-starter-validation'
        }

        dependencySet(group: 'org.springframework', version: '5.3.23') {
            entry 'spring-aop'
            entry 'spring-aspects'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-context-support'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-jdbc'
            entry 'spring-jms'
            entry 'spring-messaging'
            entry 'spring-orm'
            entry 'spring-test'
            entry 'spring-tx'
            entry 'spring-web'
            entry 'spring-webmvc'
        }

        //CVE-2021-22112
        dependencySet(group: 'org.springframework.security', version: '5.7.3') {
            entry 'spring-security-crypto'
        }
    }
}
